USE Data_Warehouse;
GO

IF OBJECT_ID('silver.load_silver', 'P') IS NOT NULL
    DROP PROCEDURE silver.load_silver;
GO

CREATE PROCEDURE silver.load_silver AS
BEGIN
SET NOCOUNT ON;
    DECLARE 
        @start_time         DATETIME,
        @end_time           DATETIME,
        @start_batch_time   DATETIME,
        @end_batch_time     DATETIME;

    BEGIN TRY
        SET @start_batch_time = GETDATE();

        PRINT '=======| Loading Silver Layer |=======';
        PRINT '==================-----| Loading CRM Data |-----==================';

        /* ================= CRM CUSTOMER ================= */

        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.crm_cust_info |';
        TRUNCATE TABLE silver.crm_cust_info;

        PRINT '=======|Inserting data into table: silver.crm_cust_info|======='
		PRINT 'Result:'
		INSERT INTO silver.crm_cust_info
			(
			cst_id,
			cst_key,
			cst_firstname,
			cst_lastname,
			cst_marital_status,
			cst_gndr,
			cst_create_date
			)

		SELECT 
			cst_id,
			cst_key,
			TRIM(cst_firstname) AS cst_firstname,
			TRIM(cst_lastname) AS cst_lastname,
			CASE 
				WHEN UPPER(TRIM(cst_marital_status)) = 'M' THEN 'Married'
				WHEN UPPER(TRIM(cst_marital_status)) = 'S' THEN 'Single'
				ELSE 'n/a'
				END AS marital_status,
			CASE
				WHEN UPPER(TRIM(cst_gndr)) = 'M' THEN 'Male'
				WHEN UPPER(TRIM(cst_gndr)) = 'F' THEN 'Female'
				ELSE 'n/a'
				END AS cst_gndr,
			CAST(cst_create_date AS DATE) AS cst_create_date 
		FROM
			(
			SELECT *,
			ROW_NUMBER() OVER(PARTITION BY cst_id ORDER BY cst_create_date DESC) AS date_rank
			FROM bronze.crm_cust_info) AS t WHERE date_rank=1;
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        /* ================= CRM PRODUCT ================= */

        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.crm_prd_info |';
        TRUNCATE TABLE silver.crm_prd_info;

        PRINT '>>>>>>| Inserting Data Into: silver.crm_prd_info |';
        PRINT 'Result:'
		INSERT INTO silver.crm_prd_info
			(
			prd_id,
			prd_key,
			cat_id,
			prd_nm,
			prd_cost,
			prd_line,
			prd_start_dt,
			prd_end_dt
			)

		SELECT
			prd_id,
			SUBSTRING(prd_key, 7, LEN(prd_key)) AS prd_key,
			REPLACE(SUBSTRING(prd_key, 1,5), '-', '_') AS cat_id,
			prd_nm,
			ISNULL(prd_cost, 0) AS prd_cost,
				 CASE UPPER(TRIM(prd_line)) 
				 WHEN 'M' THEN 'Mountain'
				 WHEN 'R' THEN 'Road'
				 WHEN 'S' THEN 'Other Sales'
				 WHEN 'T' THEN 'Touring'
				 ELSE 'n/a' END AS prd_line,
			CAST(prd_start_dt AS DATE) AS prd_start_dt,
			CAST(LEAD(prd_start_dt) OVER (PARTITION BY prd_key ORDER BY prd_start_dt)-1 AS DATE) AS prd_end_dt
		FROM bronze.crm_prd_info;
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        /* ================= CRM SALES ================= */

        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.crm_sales_details |';
        TRUNCATE TABLE silver.crm_sales_details;

        PRINT '>>>>>>| Inserting Data Into: silver.crm_sales_details |';
		PRINT 'Result:'
        INSERT INTO silver.crm_sales_details
			(
			sls_ord_num,
			sls_prd_key,
			sls_cust_id,
			sls_order_dt,
			sls_ship_dt,
			sls_due_dt,
			sls_sales,
			sls_quantity,
			sls_price
			)

		SELECT
			sls_ord_num,
			sls_prd_key,
			sls_cust_id,
			CASE 
				WHEN sls_order_dt<0 OR LEN(sls_order_dt)<8 THEN NULL
				ELSE CAST(CAST(sls_order_dt AS VARCHAR) AS DATE) 
				END AS sls_order_dt,
			CASE
				WHEN sls_ship_dt<0 OR LEN(sls_ship_dt)<8 THEN NULL
				ELSE CAST(CAST(sls_ship_dt AS VARCHAR) AS DATE) 
				END AS sls_ship_dt,
			CASE
				WHEN sls_due_dt<0 OR LEN(sls_due_dt)<8 THEN NULL
				ELSE CAST(CAST(sls_due_dt AS VARCHAR) AS DATE) 
				END AS sls_due_dt,
			CASE
				WHEN sls_sales IS NULL OR sls_sales != sls_quantity*ABS(sls_price) 
				THEN sls_quantity*ABS(sls_price)
				ELSE sls_sales 
				END AS sls_sales,
			sls_quantity,
			CASE
				WHEN sls_price IS NULL OR sls_price <0
				THEN sls_sales/NULLIF(sls_quantity, 0)
				ELSE sls_price 
				END AS sls_price
		FROM bronze.crm_sales_details;
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        /* ================= ERP DATA ================= */

        PRINT '==================-----| Loading ERP Data |-----==================';

        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.erp_cust_az12 |';
        TRUNCATE TABLE silver.erp_cust_az12;

        PRINT '>>>>>>| Inserting Data Into: silver.erp_cust_az12 |';
		PRINT 'Result:'
        INSERT INTO silver.erp_cust_az12
			(
			CID,
			BDATE,
			GEN
			)

		SELECT
			CASE
				WHEN CID LIKE 'NAS%'
				THEN SUBSTRING(CID, 4, LEN(CID))
				ELSE CID
				END AS CID,
			CASE
				WHEN BDATE> GETDATE() THEN NULL
				ELSE CAST(BDATE AS DATE)
				END AS BDATE,
			CASE 
				WHEN UPPER(TRIM(GEN)) IN ('F', 'FEMALE')  THEN 'Female'
				WHEN UPPER(TRIM(GEN)) IN ('M', 'MALE') THEN 'Male'
				ELSE 'n/a' 
				END AS GEN
		FROM bronze.erp_cust_az12
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.erp_loc_a101 |';
        TRUNCATE TABLE silver.erp_loc_a101;

        PRINT '>>>>>>| Inserting Data Into: silver.erp_loc_a101 |';
		PRINT 'Result:'
        INSERT INTO silver.erp_loc_a101
			(
			cid,
			cntry
			)

		SELECT
			REPLACE(CID, '-', '') AS cid,
			CASE
				WHEN TRIM(CNTRY) ='' OR CNTRY IS NULL THEN 'n/a'
				WHEN TRIM(CNTRY) ='DE' THEN 'Germany'
				WHEN TRIM(CNTRY) IN ('US', 'USA') THEN 'United States'
				ELSE CNTRY
				END AS cntry
		FROM bronze.erp_loc_a101;
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        SET @start_time = GETDATE();
        PRINT '>>>>>>| Truncating table: silver.erp_px_cat_g1v2 |';
        TRUNCATE TABLE silver.erp_px_cat_g1v2;

        PRINT '>>>>>>| Inserting Data Into: silver.erp_px_cat_g1v2 |';
		PRINT 'Result:'
        INSERT INTO silver.erp_px_cat_g1v2
			(
			id,
			cat,
			subcat,
			maintenance
			)

		SELECT
			ID,
			CAT,
			SUBCAT,
			MAINTENANCE
		FROM bronze.erp_px_cat_g1v2;
        SET @end_time = GETDATE();
        PRINT '>>>>>>| Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_time, @end_time) AS NVARCHAR(20)) 
             + ' seconds';
        PRINT '___________________';


        /* ================= BATCH TOTAL TIME ================= */

        SET @end_batch_time = GETDATE();
        PRINT '>>>>>>| Batch Load Duration: '
             + CAST(DATEDIFF(SECOND, @start_batch_time, @end_batch_time) AS NVARCHAR(20)) 
             + ' seconds';

    END TRY
    BEGIN CATCH
        PRINT '===========================';
        PRINT 'ERROR OCCURED DURING LOADING SILVER LAYER';
        PRINT 'ERROR MESSAGE : ' + ERROR_MESSAGE();
        PRINT 'ERROR NUMBER  : ' + CAST(ERROR_NUMBER() AS NVARCHAR(20));
        PRINT 'ERROR STATE   : ' + CAST(ERROR_STATE() AS NVARCHAR(20));
        PRINT '===========================';
    END CATCH
END
GO
